@rem -------------------------------------------------------------------------
@rem  filename      :   table2javabean.cmd
@rem  maintainer    :   Zhihua Lee < zhihua_lee AT sina DOT com >
@rem  created date  :   16 Jan , 2014
@rem  last modified :   17 Jan , 2014
@rem  description   :   This script is passed through under mysql-5.6.15  on Win7(32bit)
@rem                :   Rource file generated by this script is under working directory. If exist same file, it will be overrided.
@rem  requirement   :   Put mysql.exe in PATH
@rem -------------------------------------------------------------------------
@echo off
setlocal
chcp 65001

rem  if /I "%1"=="DEBUG" (set debug=true)

rem  set default database 
set database=cloud

@rem  recieve  table name,class name ; if database is not set , recieve it too.
if "%database%"=="" (
                     set /p database=Enter database name : 
                    )
set /p table=Enter table name : 
set /p class=Enter class name : 

@rem  set mysql connection parameter
set host=localhost
set port=3306
set user=root
set mysql_cmd=mysql -h %host% -P %port% -u %user% -p information_schema  -e "select column_name, column_type from columns where table_schema='%database%' and table_name='%table%'"

@rem  set file to store result data
set tempfile=delete_it_later.txt
set srcfile=%class%.java

@rem  remove source file if exist
if exist %srcfile% del %srcfile%

@rem  execute sql query
%mysql_cmd% >> %tempfile%

echo public Class  %class%  >> %srcfile%
echo {  >> %srcfile%

@rem  parse column name and colunm type
for /f "tokens=1,2 skip=1" %%a in (%tempfile%) do call :ParseProc %%a %%b

echo } >> %srcfile%
goto End

:ParseProc
set name=%1
set type=%2
if not %type:date=%==%type%       echo     public DateTime %name%; >> %srcfile%
if not %type:char=%==%type%       echo     public String %name%; >> %srcfile%
if not %type:bigint=%==%type%     echo     public Long %name%; >> %srcfile%
if not "%type:int(1)=%"=="%type%" echo     public Boolean %name%; >> %srcfile%
if not %type:int=%==%type%        echo     public Integer %name%; >> %srcfile%
goto :eof

:End
rem  if /I "%debug%"=="true"  type %tempfile%
type %srcfile%
del %tempfile%
endlocal
@echo on
