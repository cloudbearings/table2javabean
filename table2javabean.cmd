@rem -------------------------------------------------------------------------
@rem  filename      :   table2javabean.cmd
@rem  maintainer    :   Zhihua Lee < googol.zh AT gmail DOT com >
@rem  created date  :   16 Jan , 2014
@rem  last modified :   20 Jan , 2014
@rem  description   :   This script is passed through under mysql-5.6.15  on Win7(32bit)
@rem                :   Rource file generated by this script is under working directory. If exist same file, it will be overrided.
@rem                :   connection paramter can be customized by config file named default.config
@rem  requirement   :   Put mysql.exe in PATH
@rem -------------------------------------------------------------------------
@echo off
setlocal

set config=default.config
@rem read connection parameter from config file
for /f "tokens=1-2 skip=1 delims==" %%a in (%config%) do call :ReadConfig %%a %%b

@rem  recieve  table name,class name ; if database is not set , recieve it too.
if "%database%"=="" (
                     set /p database=Enter database name : 
                    )
set /p table=Enter table name : 
set /p class=Enter class name : 


@rem  set mysql connection parameter
set mysql_cmd=mysql -h %host% -P %port% -u %user% -p information_schema  -e "select column_name, column_type from columns where table_schema='%database%' and table_name='%table%'"

@rem  set file to store result data
set tempfile=delete_it_later.txt
set srcfile=%class%.java

@rem  remove source file if exist
if exist %srcfile% del %srcfile%

@rem  execute sql query
%mysql_cmd% >> %tempfile%

echo public Class  %class%  >> %srcfile%
echo {  >> %srcfile%

@rem  parse column name and colunm type
for /f "tokens=1,2 skip=1" %%a in (%tempfile%) do call :Procesing %%a %%b

echo } >> %srcfile%
goto End

:ReadConfig
set key=%1
set value=%2
set key=%key: ==%
set value=%value: ==%
if not "%key:host=%"=="%key%"  set host=%value%
if not "%key:port=%"=="%key%"  set port=%value%
if not "%key:user=%"=="%key%"  set user=%value%
if not "%key:database=%"=="%key%"  set database=%value%

goto :eof

:Procesing
set name=%1
set type=%2
if not %type:date=%==%type%       echo     public Date %name%; >> %srcfile%
if not %type:char=%==%type%       echo     public String %name%; >> %srcfile%
if not %type:bigint=%==%type%     echo     public Long %name%; >> %srcfile%
if not "%type:int(1)=%"=="%type%" echo     public Boolean %name%; >> %srcfile%
if not %type:int=%==%type%        echo     public Integer %name%; >> %srcfile%
goto :eof

:End
type %srcfile%
del %tempfile%

endlocal
@echo on
