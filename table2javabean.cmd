@rem -------------------------------------------------------------------------
@rem  filename      :   table2javabean.cmd
@rem  maintainer    :   Zhihua Lee < googol.zh AT gmail DOT com >
@rem  created date  :   16 Jan , 2014
@rem  last modified :   20 Jan , 2014
@rem  description   :   This script is passed through under mysql-5.6.15  on Win7(32bit)
@rem                :   Rource file generated by this script is under working directory. If exist same file, it will be overrided.
@rem                :   connection paramter can be customized by config file named default.config
@rem  requirement   :   Put mysql.exe in PATH
@rem -------------------------------------------------------------------------
@echo off
setlocal

where mysql 1>NUL 2>&1
if not errorlevel 0 goto MYSQL_NOT_FOUND

set config=default.ini
set tempFile=.temp
set bodyOfFile=.body

@rem read connection parameter from config file
for /f "tokens=1-2 skip=1 delims==" %%a in (%config%) do call :FUNC_READ_CONFIG %%a %%b

@rem  recieve  table name,class name ; if database is not set , recieve it too.
if "%database%"=="" (
                     set /p database=Enter database name : 
                    )
set /p table=Enter table name : 
set /p class=Enter class name : 


@rem  set mysql connection parameter
set mysql_cmd=mysql -h %host% -P %port% -u %user% -p information_schema  -e "select column_name, column_type from columns where table_schema='%database%' and table_name='%table%'"

@rem  execute sql query
%mysql_cmd% >> %tempFile%

echo public Class  %class%  >> %bodyOfFile%
echo {  >> %bodyOfFile%

@rem  parse column name and colunm type
for /f "tokens=1,2 skip=1" %%a in (%tempFile%) do call :FUNC_PROCESSING %%a %%b

echo } >> %bodyOfFile%

set srcFile=%class%.java
@rem  remove source file if exist
if exist %srcFile% del %srcFile%
if "%shouldImportDate%"=="1" echo import java.util.Date; >> %srcFile%
type %bodyOfFile% >> %srcFile%
del %tempFile%
del %bodyOfFile%

goto End

:FUNC_READ_CONFIG
set key=%1
set value=%2
set key=%key: ==%
set value=%value: ==%
if not "%key:host=%"=="%key%"  set host=%value%
if not "%key:port=%"=="%key%"  set port=%value%
if not "%key:user=%"=="%key%"  set user=%value%
if not "%key:database=%"=="%key%"  set database=%value%
goto :eof

:FUNC_PROCESSING
set name=%1
set type=%2
if not "%type:date=%"=="%type%"       echo     public Date %name%; >> %bodyOfFile% & set shouldImportDate=1
if not "%type:char=%"=="%type%"       echo     public String %name%; >> %bodyOfFile%
if not "%type:bigint=%"=="%type%"     echo     public Long %name%; >> %bodyOfFile%
if not "%type:int(1)=%"=="%type%" echo     public Boolean %name%; >> %bodyOfFile%
if not "%type:int=%"=="%type%"        echo     public Integer %name%; >> %bodyOfFile%
if not "%type:tinyblob=%"=="%type%"        echo     public String %name%; >> %bodyOfFile%
if not "%type:tinyint=%"=="%type%"        echo     public Integer %name%; >> %bodyOfFile%
if not "%type:decimal=%"=="%type%"        echo     public BigDecimal %name%; >> %bodyOfFile%
if not "%type:timestamp=%"=="%type%"        echo     public TimeStamp %name%; >> %bodyOfFile%
if not "%type:enum=%"=="%type%"        echo     public String %name%; >> %bodyOfFile%
goto :eof

:MYSQL_NOT_FOUND
echo [ERROR] mysql.exe not found in PATH .
goto END

:End
endlocal
@echo on
